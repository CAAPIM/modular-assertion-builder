/*
 * Copyright (c) 2017 CA. All rights reserved.
 * This software may be modified and distributed under the terms
 * of the MIT license.  See the LICENSE file for details.
 */
plugins {
    id "com.gradle.plugin-publish" version "0.10.0"
    id "org.sonarqube" version "2.6.2"
    id "com.srcclr.gradle" version "3.0.3"
    id "com.jfrog.bintray" version "1.8.4"
}

description = 'The modular assertion builder plugin can be used to build modular assertions for the CA API Gateway'
group 'com.ca.apim.gateway'
tag {
    message {
        "version: ${version} build-date: " + new Date().format('yyyy-MM-dd\'T\'HH:mm:ss')
    }
}
project.ext.'gradle.publish.key' = System.env.GRADLE_PUBLISH_KEY
project.ext.'gradle.publish.secret' = System.env.GRADLE_PUBLISH_SECRET

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'com.srcclr.gradle'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile gradleApi()
    testCompile 'junit:junit:4.12'
}

repositories {
    mavenCentral()
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar, sourceJar
}

// Create the pom configuration:
def pomConfig = {
    scm {
        url 'https://github.com/ca-api-gateway/modular-assertion-builder'
    }

    licenses {
        license {
            name 'MIT'
            url 'https://github.com/ca-api-gateway/modular-assertion-builder/blob/master/LICENSE'
        }
    }

    developers {
        developer {
            id 'apigatewayopensource'
            name 'CA API Gateway Bot'
            email 'API-Gateway-OpenSource@ca.com'
        }
    }
}

publishing {
    publications {
        BinTrayPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.description)
                root.appendNode('name', project.name)
                root.appendNode('url', 'https://github.com/ca-api-gateway/modular-assertion-builder')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['BinTrayPublication']
    publish = true
    pkg {
        repo = 'gateway-developer-plugin'
        userOrg = 'ca-api-gateway'
        name = project.name
        desc = project.description
        licenses = ['MIT']
        websiteUrl = 'https://github.com/ca-api-gateway/modular-assertion-builder'
        issueTrackerUrl = 'https://waffle.io/ca-api-gateway/modular-assertion-builder'
        vcsUrl = 'https://github.com/ca-api-gateway/modular-assertion-builder.git'
        githubRepo = 'ca-api-gateway/modular-assertion-builder'
        version {
            name = version
            released = new Date()
            vcsTag = 'release/' + version
            gpg {
                sign = true
            }
            mavenCentralSync {
                user = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSS_USER') //OSS user token: mandatory
                password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSS_PASSWORD') //OSS user password: mandatory
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/ca-api-gateway/modular-assertion-builder'
    vcsUrl = 'https://github.com/ca-api-gateway/modular-assertion-builder'

    plugins {
        greetingsPlugin {
            id = 'com.ca.apim.gateway.modular-assertion-builder'
            displayName = 'CA API Gateway Modular Assertion Builder Gradle Plugin'
            description = 'The modular assertion builder plugin can be used to build modular assertions for the CA API Gateway'
            tags = ['ca api gateway','building']
        }
    }
}
